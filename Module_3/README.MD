# Tokens - ERC20, ERC721

https://docs.soliditylang.org/en/v0.7.4/

## 1. ERC20 token 

Open Remix IDE && create `<your_token_name.sol>` 

```js
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import "@openzeppelin/contracts/token/ERC20/ERC20.sol";

contract YOUR_TOKEN is ERC20 {
    constructor(uint256 initialSupply) ERC20("NAME", "TICKER") {
        _mint(msg.sender, initialSupply);
    }
    function decimals() public view virtual override returns (uint8) {
        return 2;
    }
}
```
Deployed FIN2020 ERC20 token address
`0xe2BC1b08aC777bd6FBA839B863Bb885A31De549b`

## 2. NFT (ERC721 Token)

https://eips.ethereum.org/EIPS/eip-721

https://docs.openzeppelin.com/contracts/4.x/erc721

#### 1. Upload assets to IPFS storage and create an API

- go to https://nft.storage/: register and login
- upload files at https://nft.storage/files/
- create an API key at https://nft.storage/manage/


#### 2. Create easy avaliable `json` metadata at https://my-json-server.typicode.com

- go to github.com and create a new repo
- in this repo create a file `db.json`
- check avaliability at http://my-json-server.typicode.com/[your_name]/[repo]/tokens/

Sample [db.json]

```json
{
    "tokens": [
    {
      "id": 0,
      "description": "FINTECH 2022 NFT",
      "external_url": "https://github.com/pavelkrolevets/blockchain_course/tree/main/Module_2",
      "image": "https://bafkreih5a7q3byh76our22se7vhhpkp67vabpzdukdhs633zkvka7klxsi.ipfs.dweb.link/",
      "name": "FINTECH 2022 NFT 0"
    },
    {
      "id": 1,
      "description": "FINTECH 2022 NFT",
      "external_url": "https://github.com/pavelkrolevets/blockchain_course/tree/main/Module_2",
      "image": "https://drive.google.com/file/d/0Bz448ORkyS2eSFc1ZUh3bUpubWs/view?usp=sharing&resourcekey=0-ABtvc0gJBxLDTKPgZWW1Yg",
      "name": "FINTECH 2022 NFT 1"
    },
    {
      "id": 2,
      "description": "FINTECH 2022 NFT",
      "external_url": "https://github.com/pavelkrolevets/blockchain_course/tree/main/Module_2",
      "image": "https://twemoji.maxcdn.com/svg/1f40e.svg",
      "name": "FINTECH 2022 NFT 2"
    }
  ]
}
```




#### 3. Deploy an NFT smart contract ("ERC721PresetMinterPauserAutoId")

* baseURI: https://my-json-server.typicode.com/pavelkrolevets/blockchain_course/tokens/

```js
// SPDX-License-Identifier: MIT

pragma solidity >=0.6.0 <0.8.0;

import "https://github.com/OpenZeppelin/openzeppelin-contracts/blob/v3.4.0/contracts/access/AccessControl.sol";
import "https://github.com/OpenZeppelin/openzeppelin-contracts/blob/v3.4.0/contracts/utils/Context.sol";
import "https://github.com/OpenZeppelin/openzeppelin-contracts/blob/v3.4.0/contracts/utils/Counters.sol";
import "https://github.com/OpenZeppelin/openzeppelin-contracts/blob/v3.4.0/contracts/token/ERC721/ERC721.sol";
import "https://github.com/OpenZeppelin/openzeppelin-contracts/blob/v3.4.0/contracts/token/ERC721/ERC721Burnable.sol";
import "https://github.com/OpenZeppelin/openzeppelin-contracts/blob/v3.4.0/contracts/token/ERC721/ERC721Pausable.sol";

/**
 * @dev {ERC721} token, including:
 *
 *  - ability for holders to burn (destroy) their tokens
 *  - a minter role that allows for token minting (creation)
 *  - a pauser role that allows to stop all token transfers
 *  - token ID and URI autogeneration
 *
 * This contract uses {AccessControl} to lock permissioned functions using the
 * different roles - head to its documentation for details.
 *
 * The account that deploys the contract will be granted the minter and pauser
 * roles, as well as the default admin role, which will let it grant both minter
 * and pauser roles to other accounts.
 */
contract ERC721PresetMinterPauserAutoId is Context, AccessControl, ERC721Burnable, ERC721Pausable {
    using Counters for Counters.Counter;

    bytes32 public constant MINTER_ROLE = keccak256("MINTER_ROLE");
    bytes32 public constant PAUSER_ROLE = keccak256("PAUSER_ROLE");

    Counters.Counter private _tokenIdTracker;

    /**
     * @dev Grants `DEFAULT_ADMIN_ROLE`, `MINTER_ROLE` and `PAUSER_ROLE` to the
     * account that deploys the contract.
     *
     * Token URIs will be autogenerated based on `baseURI` and their token IDs.
     * See {ERC721-tokenURI}.
     */
    constructor(string memory name, string memory symbol, string memory baseURI) public ERC721(name, symbol) {
        _setupRole(DEFAULT_ADMIN_ROLE, _msgSender());

        _setupRole(MINTER_ROLE, _msgSender());
        _setupRole(PAUSER_ROLE, _msgSender());

        _setBaseURI(baseURI);
    }

    /**
     * @dev Creates a new token for `to`. Its token ID will be automatically
     * assigned (and available on the emitted {IERC721-Transfer} event), and the token
     * URI autogenerated based on the base URI passed at construction.
     *
     * See {ERC721-_mint}.
     *
     * Requirements:
     *
     * - the caller must have the `MINTER_ROLE`.
     */
    function mint(address to) public virtual {
        require(hasRole(MINTER_ROLE, _msgSender()), "ERC721PresetMinterPauserAutoId: must have minter role to mint");

        // We cannot just use balanceOf to create the new tokenId because tokens
        // can be burned (destroyed), so we need a separate counter.
        _mint(to, _tokenIdTracker.current());
        _tokenIdTracker.increment();
    }

    /**
     * @dev Pauses all token transfers.
     *
     * See {ERC721Pausable} and {Pausable-_pause}.
     *
     * Requirements:
     *
     * - the caller must have the `PAUSER_ROLE`.
     */
    function pause() public virtual {
        require(hasRole(PAUSER_ROLE, _msgSender()), "ERC721PresetMinterPauserAutoId: must have pauser role to pause");
        _pause();
    }

    /**
     * @dev Unpauses all token transfers.
     *
     * See {ERC721Pausable} and {Pausable-_unpause}.
     *
     * Requirements:
     *
     * - the caller must have the `PAUSER_ROLE`.
     */
    function unpause() public virtual {
        require(hasRole(PAUSER_ROLE, _msgSender()), "ERC721PresetMinterPauserAutoId: must have pauser role to unpause");
        _unpause();
    }

    function _beforeTokenTransfer(address from, address to, uint256 tokenId) internal virtual override(ERC721, ERC721Pausable) {
        super._beforeTokenTransfer(from, to, tokenId);
    }
}
```
#### 4. Mint to an adress to preserve ownership using `mint` function at the contract



#### 5. Validate NFT at `https://rinkeby-api.opensea.io/asset/[nft contract address]/[token id]/validate`

My example https://rinkeby-api.opensea.io/asset/rinkeby/0x53f209A2B1C995c135401cFD1C6499Ad7704f76D/0/validate

#### 6. Check you NFT at OpenSea.io https://rinkeby.opensea.io/assets/[nft contract address]/[token id]

My example https://testnets.opensea.io/assets/rinkeby/0x53f209A2B1C995c135401cFD1C6499Ad7704f76D/0

More info here:

https://forum.openzeppelin.com/t/create-an-nft-and-deploy-to-a-public-testnet-using-remix/6358